# This snakefile runs genetic demultiplexing for all of our pooled data across all reference genotypes.

# ---------
# Variables
# ---------

# Samples: the eight samples used in the pilot. These samples were bulk and single-cell sequenced several different ways.
# Pools: we ran each sample separately but also pooled them into two sets of four. Pools are named after the date they were prepared.
# Pool map: which samples are part of which pool. This is used to merge bam files per pool.
# Bulk types: we ran bulk RNA-seq on the samples in three separate ways: rRNA depletion on tumor chunks, dissociation and then rRNA
# depletion, dissociation and then polyA (3') capture. Here, each kind of bulk data can be used to generate reference genotypes.

SAMPLES = ['2251', '2267', '2283', '2293', '2380', '2428', '2467', '2497']
POOLS = ['12162021', '01132022']
POOL_MAP = {'12162021':['2267', '2283', '2293', '2380'], '01132022':['2251', '2428', '2467', '2497']}
BULK_TYPES = ['chunk_ribo', 'dissociated_ribo', 'dissociated_polyA']
THREADS = 6 #TODO: figure out if this is necessary

dir_pooled = "../../sc-cancer-hgsc/data/pooled_tumors"
dir_bulk = "../../sc-cancer-hgsc/data/bulk_tumors"
dir_index = "../../sc-cancer-hgsc/data/index"

wildcard_constraints:
	pool="\d+",
	bulk_type="[A-Za-z]+_[A-Za-z]+"

# --------
# Workflow
# --------

def get_pool_samples(pool):
	return TMP[pool]

rule all:
	input:
		expand(dir_pooled + "/{pool}/vireo/{bulk_type}/donor_ids.tsv", pool=POOLS, bulk_type=BULK_TYPES)

rule run_vireo:
	input:
		dir_pooled + "/{pool}/cellSNP/{bulk_type}/cellSNP.base.vcf.gz"
	output:
		dir_pooled + "/{pool}/vireo/{bulk_type}/donor_ids.tsv"
	shell:
		"bash 06_run_vireo.sh {wildcards.pool} {wildcards.bulk_type}"

rule run_cellSNP:
	input:
		dir_pooled + "/{pool}/bam/pooled.bam",
		"barcodes_{pool}.txt",
		dir_bulk + "/pooled_vcf/bcftools_{pool}_{bulk_type}_rehead.vcf"
	output:
		dir_pooled + "/{pool}/cellSNP/{bulk_type}/cellSNP.base.vcf.gz"
	shell:
		"bash 05_run_cellsnp.sh {wildcards.pool} {wildcards.bulk_type}"

rule make_vcf:
	input:
		bam = lambda wildcards: \
			[dir_bulk + "/{0}/{1}/STAR/Aligned.sortedByCoord.out.bam".format(sampleid, wildcards.bulk_type) \
			for sampleid in POOL_MAP[wildcards.pool]
			],
		genome = dir_index + "/refdata-gex-GRCh38-2020-A/fasta/genome.fa"
	output:
		dir_bulk + "/pooled_vcf/bcftools_{pool}_{bulk_type}_rehead.vcf"
	shell:
		"bash 02_make_bulk_vcf_file.sh {wildcards.pool} {wildcards.bulk_type}"

rule make_bulk_bam:
	input:
		ancient(dir_index + "/STAR_index/SAindex"),
		r1 = dir_bulk + "/{sample}/{bulk_type}/{sample}_R1_merged.fastq.gz",
		r2 = dir_bulk + "/{sample}/{bulk_type}/{sample}_R2_merged.fastq.gz"
	params:
		index = dir_index + "/STAR_index",
		outdir = dir_bulk + "/{sample}/{bulk_type}/STAR/"
	output:
		dir_bulk + "/{sample}/{bulk_type}/STAR/Aligned.sortedByCoord.out.bam"
	shell:
		"""STAR \
		--genomeDir {params.index} \
		--runThreadN {THREADS} \
		--readFilesIn {input.r1} {input.r2} \
		--outFileNamePrefix {params.outdir} \
		--readFilesCommand gunzip -c \
		--outSAMtype BAM SortedByCoordinate \
		--quantMode GeneCounts
		samtools index {output}"""

rule merge_bulk_fastas:
	input:
		l1r1 = dir_bulk + "/{sample}/{bulk_type}/{sample}_L001_R1_001.fastq.gz",
		l1r2 = dir_bulk + "/{sample}/{bulk_type}/{sample}_L001_R2_001.fastq.gz",
		l2r1 = dir_bulk + "/{sample}/{bulk_type}/{sample}_L002_R1_001.fastq.gz",
		l2r2 = dir_bulk + "/{sample}/{bulk_type}/{sample}_L002_R2_001.fastq.gz"
	output:
		r1 = dir_bulk + "/{sample}/{bulk_type}/{sample}_R1_merged.fastq.gz",
		r2 = dir_bulk + "/{sample}/{bulk_type}/{sample}_R2_merged.fastq.gz"
	shell:
		"""cat {input.l1r1} {input.l2r1} > {output.r1}
		cat {input.l1r2} {input.l2r2} > {output.r2}"""	

rule get_sc_barcodes:
	output:
		temp("barcodes_{pool}.txt")
	params:
		assignmentsfile = dir_pooled + "/{pool}/Cellranger/outs/multi/multiplexing_analysis/assignment_confidence_table.csv"
	shell:
		"cut {params.assignmentsfile} -d',' -f6 | sed '1d' > {output}"

rule merge_sc_bam:
	output:
		dir_pooled + "/{pool}/bam/pooled.bam"
	params:
		unassigned = dir_pooled + "/{pool}/Cellranger/outs/multi/count/unassigned_alignments.bam",
		assigned = lambda wildcards: \
			[dir_pooled + "/{0}/Cellranger/outs/per_sample_outs/{1}/count/sample_alignments.bam".format(wildcards.pool, sampleid) \
			for sampleid in POOL_MAP[wildcards.pool]
			]
	shell:
		"samtools merge {output} {params.unassigned} {params.assigned}; samtools index {output}"

rule make_star_index:
	input:
		dir_index + "/refdata-gex-GRCh38-2020-A/fasta/genome.fa",
		dir_index + "/refdata-gex-GRCh38-2020-A/genes/genes.gtf"
	output:
		dir_index + "/STAR_index/SAindex"
	shell:
		"STAR --runMode genomeGenerate --runThreadN 6 --genomeDir {dir_index}/STAR_index --genomeFastaFiles {dir_index}/refdata-gex-GRCh38-2020-A/fasta/genome.fa --sjdbGTFfile {dir_index}/refdata-gex-GRCh38-2020-A/genes/genes.gtf"

rule get_reference_genome:
	output:
		dir_index + "/refdata-gex-GRCh38-2020-A.tar.gz"
	shell:
		"wget -P dir_index https://cf.10xgenomics.com/supp/cell-exp/refdata-gex-GRCh38-2020-A.tar.gz"

checkpoint decompress_reference_genome:
	output:
		directory(dir_index + "refdata-gex-GRCh38-2020-A")
	input:
		dir_index + "/refdata-gex-GRCh38-2020-A.tar.gz"
	shell:
		"tar xvf {input} -C {output}"
